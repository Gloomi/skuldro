//===== rAthena Script =======================================
//= Reset NPC
//===== Description: =========================================
//= Resets skills, stats, or both.
//===== Additional Comments: =================================
//= 1.0 First Version
//= 1.1 Optimized for the greater good. [Kisuka]
//= 1.2 Cleaning [Euphy]
//= 1.3 All statuses removed upon skill reset. [Euphy]
//= 1.4 Compressed Script, Added limit use option [Stolao]
//=	Changed set -> setarray, Improved text with F_InsertPlural
//= 1.5 Added sc_end_class to reset related status changes [sader1992]
//============================================================
prt_in,46,97,4	script	Strange Novice	567,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	1000000,	1000000,	1500000,	0;
	mes "[Strange Novice]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Sorry you can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life.";
		close;
	}
	mes "Trust me, I know what you're thinking.";
	next;
	mes "[Strange Novice]";
	mes "Well everyone can't help but wonder...";
	next;
	mes "[Strange Novice]";
	mes "'what if I chose my path differently?'";
	next;
	mes "[Strange Novice]";
	mes "I can help you rewrite your mistakes...";
	next;
	mes "[Strange Novice]";
	mes "for a price.";
	next;
	mes "[Strange Novice]";
	mes "Now tell me, what do you seek?";
	next;
	set .@i,(select("^FF3355Reset Skills:Reset Stats:Reset Both^000000:Cancel"));
	if(.@i > 3) close;
	mes "[Strange Novice]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "I can't help you under that condition.";
		close;
	}
	if(.@Reset[3]){
		mes "You can only reset "+callfunc("F_InsertPlural",.@Reset[3],"time")+" in your life, are you sure?";
		if(select("Let me think:That's fine") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "I hope this time you will make things right.";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
